// RUN: dyn-opt %s | dyn-opt | FileCheck %s

irdlssa.dialect testd {
  // CHECK: irdlssa.type parametric {
  // CHECK:   %[[v0:[^ ]*]] = irdlssa.any_type
  // CHECK:   irdlssa.parameters(%[[v0]])
  // CHECK: }
  irdlssa.type parametric {
    %0 = irdlssa.any_type
    irdlssa.parameters(%0)
  }

  // CHECK: irdlssa.operation eq {
  // CHECK:   %[[v0:[^ ]*]] = irdlssa.is_type : i32
  // CHECK:   irdlssa.results(%[[v0]])
  // CHECK: }
  irdlssa.operation eq {
    %0 = irdlssa.is_type : i32
    irdlssa.results(%0)
  }

  // CHECK: irdlssa.operation eq_param {
  // CHECK:   %[[v0:[^ ]*]] = irdlssa.is_type : "testd.parametric"<i32>
  // CHECK:   irdlssa.results(%[[v0]])
  // CHECK: }
  irdlssa.operation eq_param {
    %0 = irdlssa.is_type : "testd.parametric"<i32>
    irdlssa.results(%0)
  }

  // CHECK: irdlssa.operation anyof {
  // CHECK:   %[[v0:[^ ]*]] = irdlssa.is_type : i32
  // CHECK:   %[[v1:[^ ]*]] = irdlssa.is_type : i64
  // CHECK:   %[[v2:[^ ]*]] = irdlssa.any_of(%[[v0]], %[[v1]])
  // CHECK:   irdlssa.results(%[[v2]])
  // CHECK: }
  irdlssa.operation anyof {
    %0 = irdlssa.is_type : i32
    %1 = irdlssa.is_type : i64
    %2 = irdlssa.any_of(%0, %1)
    irdlssa.results(%2)
  }

  // CHECK: irdlssa.operation any {
  // CHECK:   %[[v0:[^ ]*]] = irdlssa.any_type
  // CHECK:   irdlssa.results(%[[v0]])
  // CHECK: }
  irdlssa.operation any {
    %0 = irdlssa.any_type
    irdlssa.results(%0)
  }

  // CHECK: irdlssa.operation dynbase {
  // CHECK:   %[[v0:[^ ]*]] = irdlssa.any_type
  // CHECK:   %[[v1:[^ ]*]] = irdlssa.parametric_type : "testd.parametric"<%[[v0]]>
  // CHECK:   irdlssa.results(%[[v1]])
  // CHECK: }
  irdlssa.operation dynbase {
    %0 = irdlssa.any_type
    %1 = irdlssa.parametric_type : "testd.parametric"<%0>
    irdlssa.results(%1)
  }

  // CHECK: irdlssa.operation dynparams {
  // CHECK:   %[[v0:[^ ]*]] = irdlssa.is_type : i32
  // CHECK:   %[[v1:[^ ]*]] = irdlssa.is_type : i64
  // CHECK:   %[[v2:[^ ]*]] = irdlssa.any_of(%[[v0]], %[[v1]])
  // CHECK:   %[[v3:[^ ]*]] = irdlssa.parametric_type : "testd.parametric"<%[[v2]]>
  // CHECK:   irdlssa.results(%[[v3]])
  // CHECK: }
  irdlssa.operation dynparams {
    %0 = irdlssa.is_type : i32
    %1 = irdlssa.is_type : i64
    %2 = irdlssa.any_of(%0, %1)
    %3 = irdlssa.parametric_type : "testd.parametric"<%2>
    irdlssa.results(%3)
  }

  // CHECK: irdlssa.operation params {
  // CHECK:   %[[v0:[^ ]*]] = irdlssa.is_type : i32
  // CHECK:   %[[v1:[^ ]*]] = irdlssa.is_type : i64
  // CHECK:   %[[v2:[^ ]*]] = irdlssa.any_of(%[[v0]], %[[v1]])
  // CHECK:   %[[v3:[^ ]*]] = irdlssa.parametric_type : "std.complex"<%[[v2]]>
  // CHECK:   irdlssa.results(%[[v3]])
  // CHECK: }
  irdlssa.operation params {
    %0 = irdlssa.is_type : i32
    %1 = irdlssa.is_type : i64
    %2 = irdlssa.any_of(%0, %1)
    %3 = irdlssa.parametric_type : "std.complex"<%2>
    irdlssa.results(%3)
  }

  // CHECK: irdlssa.operation constraint_vars {
  // CHECK:   %[[v0:[^ ]*]] = irdlssa.is_type : i32
  // CHECK:   %[[v1:[^ ]*]] = irdlssa.is_type : i64
  // CHECK:   %[[v2:[^ ]*]] = irdlssa.any_of(%[[v0]], %[[v1]])
  // CHECK:   irdlssa.results(%[[v2]], %[[v2]])
  // CHECK: }
  irdlssa.operation constraint_vars {
    %0 = irdlssa.is_type : i32
    %1 = irdlssa.is_type : i64
    %2 = irdlssa.any_of(%0, %1)
    irdlssa.results(%2, %2)
  }
}
